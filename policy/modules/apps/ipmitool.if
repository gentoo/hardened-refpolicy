## <summary>Intelligent Platform Management Interface (IPMI) Tool</summary>

########################################
## <summary>
##	Execute the ipmitool program in
##	the ipmitool domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`ipmitool_domtrans',`
	gen_require(`
		type ipmitool_t, ipmitool_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, ipmitool_exec_t, ipmitool_t)
')

########################################
## <summary>
##	Transition to ipmitool_t when NNP has been set
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`ipmitool_nnp_domtrans',`
	gen_require(`
		type ipmitool_t;
	')

	ipmitool_domtrans($1)
	allow $1 ipmitool_t:process2 nnp_transition;
')

########################################
## <summary>
##	Execute ipmitool in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`ipmitool_exec',`
	gen_require(`
		type ipmitool_exec_t;
	')

	corecmd_search_bin($1)
	can_exec($1, ipmitool_exec_t)
')

########################################
## <summary>
##	Execute ipmitool in the
##	ipmitool domain, and allow the
##	specified role the ipmitool domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
#
interface(`ipmitool_run',`
	gen_require(`
		attribute_role ipmitool_roles;
	')

	ipmitool_domtrans($1)
	roleattribute $2 ipmitool_roles;
')

