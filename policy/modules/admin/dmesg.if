## <summary>Policy for dmesg.</summary>

########################################
## <summary>
##	Execute dmesg in the dmesg domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`dmesg_domtrans',`
	gen_require(`
		type dmesg_t, dmesg_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, dmesg_exec_t, dmesg_t)
')

########################################
## <summary>
##	Transition to dmesg_t when NNP has been set
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`dmesg_nnp_domtrans',`
	gen_require(`
		type dmesg_t;
	')

	dmesg_domtrans($1)
	allow $1 dmesg_t:process2 nnp_transition;
')

########################################
## <summary>
##	Execute dmesg in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`dmesg_exec',`
	gen_require(`
		type dmesg_exec_t;
	')

	corecmd_search_bin($1)
	can_exec($1, dmesg_exec_t)
')

# This should be in an ifdef distro_gentoo but that is not allowed in an if file

########################################
## <summary>
##	Execute dmesg in the dmesg_t domain, and allow the calling role
##	the dmesg_t domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`dmesg_run',`
	gen_require(`
		type dmesg_t;
	')

	dmesg_domtrans($1)
	role $2 types dmesg_t;
')
