## <summary>Package Management System.</summary>

########################################
## <summary>
##	Execute emerge in the portage domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`portage_domtrans',`
	gen_require(`
		type portage_t, portage_exec_t;
		type portage_tmp_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, portage_exec_t, portage_t)

	can_exec($1, portage_tmp_t) # Portage does exectest
')

########################################
## <summary>
##	Execute emerge in the portage domain,
##	and allow the specified role the
##	portage domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`portage_run',`
	gen_require(`
		attribute_role portage_roles;
	')

	portage_domtrans($1)
	roleattribute $2 portage_roles;
')

########################################
## <summary>
##	Template for portage sandbox.
## </summary>
## <desc>
##	<p>
##	Template for portage sandbox.  Portage
##	does all compiling in the sandbox.
##	</p>
## </desc>
## <param name="domain">
##	<summary>
##	Domain Allowed Access
##	</summary>
## </param>
#
interface(`portage_compile_domain',`
	gen_require(`
		class dbus send_msg;
		type portage_devpts_t, portage_log_t, portage_sandbox_t, portage_srcrepo_t;
		type portage_tmp_t, portage_tmpfs_t;
	')

	allow $1 self:capability { chown dac_override dac_read_search fowner fsetid mknod net_raw setgid setuid };
	dontaudit $1 self:capability sys_chroot;
	allow $1 self:process { dyntransition execmem getattr getcap getpgid getrlimit getsched getsession noatsecure rlimitinh setcap setfscreate setkeycreate setpgid setrlimit setsched setsockcreate share siginh signal_perms transition };
	allow $1 self:fd use;
	allow $1 self:fifo_file rw_fifo_file_perms;
	allow $1 self:shm create_shm_perms;
	allow $1 self:sem create_sem_perms;
	allow $1 self:msgq create_msgq_perms;
	allow $1 self:msg { receive send };
	allow $1 self:unix_dgram_socket create_socket_perms;
	allow $1 self:unix_stream_socket create_stream_socket_perms;
	allow $1 self:unix_dgram_socket sendto;
	allow $1 self:unix_stream_socket connectto;
	# really shouldnt need this
	allow $1 self:tcp_socket create_stream_socket_perms;
	allow $1 self:udp_socket create_socket_perms;
	# misc networking stuff (esp needed for compiling perl):
	allow $1 self:rawip_socket { create ioctl };
	# needed for merging dbus:
	allow $1 self:netlink_selinux_socket { bind create read };
	allow $1 self:dbus send_msg;

	allow $1 portage_devpts_t:chr_file { rw_chr_file_perms setattr_chr_file_perms };
	term_create_pty($1, portage_devpts_t)

	# write compile logs
	allow $1 portage_log_t:dir setattr_dir_perms;
	allow $1 portage_log_t:file { setattr_file_perms write_file_perms };

	# Support live ebuilds (-9999)
	manage_dirs_pattern($1, portage_srcrepo_t, portage_srcrepo_t)
	manage_files_pattern($1, portage_srcrepo_t, portage_srcrepo_t)
	manage_lnk_files_pattern($1, portage_srcrepo_t, portage_srcrepo_t)
	allow $1 portage_srcrepo_t:file map;

	# run scripts out of the build directory
	can_exec(portage_sandbox_t, portage_tmp_t)

	manage_dirs_pattern($1, portage_tmp_t, portage_tmp_t)
	manage_files_pattern($1, portage_tmp_t, portage_tmp_t)
	manage_lnk_files_pattern($1, portage_tmp_t, portage_tmp_t)
	manage_fifo_files_pattern($1, portage_tmp_t, portage_tmp_t)
	manage_sock_files_pattern($1, portage_tmp_t, portage_tmp_t)
	files_tmp_filetrans($1, portage_tmp_t, { dir file lnk_file sock_file fifo_file })
	# SELinux-enabled programs running in the sandbox
	allow $1 portage_tmp_t:file { relabel_file_perms map };
	allow $1 portage_tmp_t:dir relabel_dir_perms;

	manage_files_pattern($1, portage_tmpfs_t, portage_tmpfs_t)
	manage_lnk_files_pattern($1, portage_tmpfs_t, portage_tmpfs_t)
	manage_fifo_files_pattern($1, portage_tmpfs_t, portage_tmpfs_t)
	manage_sock_files_pattern($1, portage_tmpfs_t, portage_tmpfs_t)
	allow $1 portage_tmpfs_t:file map;
	fs_tmpfs_filetrans($1, portage_tmpfs_t, { dir file lnk_file sock_file fifo_file })

	kernel_read_system_state($1)
	kernel_read_network_state($1)
	kernel_read_software_raid_state($1)
	kernel_getattr_core_if($1)
	kernel_getattr_message_if($1)
	kernel_read_kernel_sysctls($1)

	corecmd_exec_all_executables($1)

	# really shouldnt need this but some packages test
	# network access, such as during configure
	# also distcc--need to reinvestigate confining distcc client
	corenet_all_recvfrom_netlabel($1)
	corenet_tcp_sendrecv_generic_if($1)
	corenet_udp_sendrecv_generic_if($1)
	corenet_raw_sendrecv_generic_if($1)
	corenet_tcp_sendrecv_generic_node($1)
	corenet_udp_sendrecv_generic_node($1)
	corenet_raw_sendrecv_generic_node($1)
	corenet_tcp_connect_all_reserved_ports($1)
	corenet_tcp_connect_distccd_port($1)
	corenet_tcp_connect_git_port($1)

	dev_read_sysfs($1)
	dev_read_rand($1)
	dev_read_urand($1)

	domain_use_interactive_fds($1)
	domain_dontaudit_read_all_domains_state($1)
	# SELinux-aware installs doing relabels in the sandbox
	domain_obj_id_change_exemption($1)

	files_exec_etc_files($1)
	files_exec_usr_src_files($1)
	files_map_usr_files($1)

	# Came up with bug #496328
	fs_getattr_tmpfs($1)
	fs_getattr_xattr_fs($1)
	fs_list_noxattr_fs($1)
	fs_read_noxattr_fs_files($1)
	fs_read_noxattr_fs_symlinks($1)
	fs_search_auto_mountpoints($1)

	selinux_validate_context($1)
	# needed for merging dbus:
	selinux_compute_access_vector($1)

	files_list_non_auth_dirs($1)
	files_read_non_auth_files($1)
	files_read_non_auth_symlinks($1)

	libs_exec_lib_files($1)
	# some config scripts use ldd
	libs_exec_ld_so($1)
	libs_exec_ldconfig($1)

	logging_send_syslog_msg($1)

	miscfiles_read_localization($1)

	userdom_use_user_terminals($1)

	# SELinux-enabled programs running in the sandbox
	seutil_libselinux_linked($1)

	# required by install
	seutil_read_file_contexts($1)

	tunable_policy(`portage_use_nfs',`
		fs_getattr_nfs($1)
		fs_manage_nfs_dirs($1)
		fs_manage_nfs_files($1)
		fs_manage_nfs_symlinks($1)
	')

	ifdef(`distro_gentoo',`
		# Fix bug 496328
		fs_getattr_tmpfs($1)
	')
')

########################################
## <summary>
##	Execute tree management functions
##	(fetching, layman, ...) in the
##	portage fetch domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`portage_domtrans_fetch',`
	gen_require(`
		type portage_fetch_t, portage_fetch_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, portage_fetch_exec_t, portage_fetch_t)
')

########################################
## <summary>
##	Execute tree management functions
##	(fetching, layman, ...) in the
##	portage fetch domain, and allow
##	the specified role the portage
##	fetch domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`portage_run_fetch',`
	gen_require(`
		attribute_role portage_fetch_roles;
	')

	portage_domtrans_fetch($1)
	roleattribute $2 portage_fetch_roles;
')

########################################
## <summary>
##	Execute gcc-config in the caller domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`portage_exec_gcc_config',`
	gen_require(`
		type gcc_config_exec_t;
	')

	corecmd_search_bin($1)
	can_exec($1, gcc_config_exec_t)
')

########################################
## <summary>
##	Execute gcc-config in the gcc config domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`portage_domtrans_gcc_config',`
	gen_require(`
		type gcc_config_t, gcc_config_exec_t;
	')

	corecmd_search_bin($1)
	nnp_domtrans_pattern($1, gcc_config_exec_t, gcc_config_t)
')

########################################
## <summary>
##	Execute gcc-config in the gcc config
##	domain, and allow the specified role
##	the gcc_config domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`portage_run_gcc_config',`
	gen_require(`
		attribute_role gcc_config_roles;
	')

	portage_domtrans_gcc_config($1)
	roleattribute $2 gcc_config_roles;
')

########################################
## <summary>
##	Do not audit attempts to use
##	portage file descriptors.
## </summary>
## <param name="domain">
##	<summary>
##	Domain to not audit.
##	</summary>
## </param>
#
interface(`portage_dontaudit_use_fds',`
	gen_require(`
		type portage_t;
	')

	dontaudit $1 portage_t:fd use;
')

########################################
## <summary>
##	Do not audit attempts to read and write inherited portage ptys.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`portage_dontaudit_use_inherited_ptys',`
	gen_require(`
		type portage_devpts_t;
	')

	dontaudit $1 portage_devpts_t:chr_file rw_inherited_term_perms;
')

########################################
## <summary>
##	Do not audit attempts to read the
##	portage configuration.
## </summary>
## <param name="domain">
##	<summary>
##	Domain to not audit.
##	</summary>
## </param>
#
interface(`portage_dontaudit_read_config',`
	gen_require(`
		type portage_conf_t;
	')

	dontaudit $1 portage_conf_t:dir search_dir_perms;
	dontaudit $1 portage_conf_t:file read_file_perms;
	dontaudit $1 portage_conf_t:lnk_file read_lnk_file_perms;
')

########################################
## <summary>
##	List the contents of portage config directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`portage_list_config',`
	gen_require(`
		type portage_conf_t;
	')

	files_search_etc($1)
	list_dirs_pattern($1, portage_conf_t, portage_conf_t)
')

########################################
## <summary>
##	List the contents of portage ebuild directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`portage_list_ebuild',`
	gen_require(`
		type portage_ebuild_t;
	')

	list_dirs_pattern($1, portage_ebuild_t, portage_ebuild_t)
')

########################################
## <summary>
##	Read portage ebuild files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`portage_read_ebuild_files',`
	gen_require(`
		type portage_ebuild_t;
	')

	read_files_pattern($1, portage_ebuild_t, portage_ebuild_t)
')

########################################
## <summary>
##	Do not audit attempts to search the
##	portage temporary directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain to not audit.
##	</summary>
## </param>
#
interface(`portage_dontaudit_search_tmp',`
	gen_require(`
		type portage_tmp_t;
	')

	dontaudit $1 portage_tmp_t:dir search_dir_perms;
')

########################################
## <summary>
##	Do not audit attempts to read and write
##	the portage temporary files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain to not audit.
##	</summary>
## </param>
#
interface(`portage_dontaudit_rw_tmp_files',`
	gen_require(`
		type portage_tmp_t;
	')

	dontaudit $1 portage_tmp_t:file rw_file_perms;
')

########################################
## <summary>
##   Allow the domain to run within an eselect module script.
## </summary>
## <param name="domain">
##     <summary>
##     Domain to allow within an eselect module
##     </summary>
## </param>
#   Specific to Gentoo,
#   eselect modules allow users to switch between different flavors or versions
#   of underlying components. In return, eselect makes a wrapper binary which
#   makes the proper selections. If this binary is different from bin_t, it might
#   not hold the necessary privileges for the wrapper to function. However, just
#   marking the target binaries doesn't always work, since for python scripts the
#   wrapper doesn't execute it, but treats the target as a library.
#
interface(`portage_eselect_module',`
       gen_require(`
               attribute portage_eselect_domain;
       ')

       typeattribute $1 portage_eselect_domain;
')

########################################
## <summary>
##	Read all portage files
## </summary>
## <param name="role">
##	<summary>
##	Role allowed access
##	</summary>
## </param>
## <param name="domain">
##	<summary>
##	Domain allowed access
##	</summary>
## </param>
#
interface(`portage_ro_role',`
	portage_read_cache($2)
	portage_read_config($2)
	portage_read_db($2)
	portage_read_ebuild($2)
	portage_read_log($2)
	portage_read_srcrepo($2)
	portage_dontaudit_write_cache($2)
')

########################################
## <summary>
##	Read portage db files
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access
##	</summary>
## </param>
#
interface(`portage_read_db',`
	gen_require(`
		type portage_db_t;
	')

	files_search_var($1)
	list_dirs_pattern($1, portage_db_t, portage_db_t)
	read_files_pattern($1, portage_db_t, portage_db_t)
')

########################################
## <summary>
##	Read portage cache files
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access
##	</summary>
## </param>
#
interface(`portage_read_cache',`
	gen_require(`
		type portage_cache_t;
	')

	files_search_var($1)
	list_dirs_pattern($1, portage_cache_t, portage_cache_t)
	read_files_pattern($1, portage_cache_t, portage_cache_t)
	read_lnk_files_pattern($1, portage_cache_t, portage_cache_t)
')

########################################
## <summary>
##	Read portage configuration files
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access
##	</summary>
## </param>
#
interface(`portage_read_config',`
	gen_require(`
		type portage_conf_t;
	')

	files_search_etc($1)
	list_dirs_pattern($1, portage_conf_t, portage_conf_t)
	read_files_pattern($1, portage_conf_t, portage_conf_t)
	allow $1 portage_conf_t:file map;
	read_lnk_files_pattern($1, portage_conf_t, portage_conf_t)
')

########################################
## <summary>
##	Read portage ebuild files
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access
##	</summary>
## </param>
#
interface(`portage_read_ebuild',`
	gen_require(`
		type portage_ebuild_t;
	')

	files_search_usr($1)
	list_dirs_pattern($1, portage_ebuild_t, portage_ebuild_t)
	read_files_pattern($1, portage_ebuild_t, portage_ebuild_t)
	allow $1 portage_ebuild_t:file map;
	read_lnk_files_pattern($1, portage_ebuild_t, portage_ebuild_t)
')

########################################
## <summary>
##	Read portage log files
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access
##	</summary>
## </param>
#
interface(`portage_read_log',`
	gen_require(`
		type portage_log_t;
	')

	logging_search_logs($1)
	read_files_pattern($1, portage_log_t, portage_log_t)
')

########################################
## <summary>
##	Read portage src repository files
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access
##	</summary>
## </param>
#
interface(`portage_read_srcrepo',`
	gen_require(`
		type portage_ebuild_t, portage_srcrepo_t;
	')

	files_search_usr($1)
	list_dirs_pattern($1, portage_ebuild_t, portage_srcrepo_t)
	read_files_pattern($1, portage_srcrepo_t, portage_srcrepo_t)
	allow $1 portage_srcrepo_t:file map;
	read_lnk_files_pattern($1, portage_srcrepo_t, portage_srcrepo_t)
')

########################################
## <summary>
##	Do not audit writing portage cache files
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access
##	</summary>
## </param>
#
interface(`portage_dontaudit_write_cache',`
	gen_require(`
		type portage_cache_t;
	')

	dontaudit $1 portage_cache_t:dir { setattr write };
')

