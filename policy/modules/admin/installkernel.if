## <summary>Install kernels and update the bootloader configuration.</summary>

########################################
## <summary>
##	Execute installkernel in the installkernel domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
#
interface(`installkernel_domtrans',`
	gen_require(`
		type installkernel_t, installkernel_exec_t;
	')

	corecmd_search_bin($1)
	domtrans_pattern($1, installkernel_exec_t, installkernel_t)
')

########################################
## <summary>
##	Execute installkernel in the installkernel
##	domain, and allow the specified
##	role the installkernel domain.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed to transition.
##	</summary>
## </param>
## <param name="role">
##	<summary>
##	Role allowed access.
##	</summary>
## </param>
## <rolecap/>
#
interface(`installkernel_run',`
	gen_require(`
		type installkernel_t;
	')

	installkernel_domtrans($1)
	role $2 types installkernel_t;
')

########################################
## <summary>
##	List the contents of installkernel tmp directories.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`installkernel_list_tmp',`
	gen_require(`
		type installkernel_tmp_t;
	')

	allow $1 installkernel_tmp_t:dir list_dir_perms;
')

########################################
## <summary>
##	Create installkernel tmp files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`installkernel_create_tmp_files',`
	gen_require(`
		type installkernel_tmp_t;
	')

	create_files_pattern($1, installkernel_tmp_t, installkernel_tmp_t)
')

########################################
## <summary>
##	Read and write installkernel tmp files.
## </summary>
## <param name="domain">
##	<summary>
##	Domain allowed access.
##	</summary>
## </param>
#
interface(`installkernel_rw_tmp_files',`
	gen_require(`
		type installkernel_tmp_t;
	')

	rw_files_pattern($1, installkernel_tmp_t, installkernel_tmp_t)
')
